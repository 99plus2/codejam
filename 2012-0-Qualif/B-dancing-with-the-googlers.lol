HAI 1.2

OBTW LOLCODE DUN HAZ YARN-SPILTING SO
YA HAFTO PREPROSHES YR INPUHT SO THERZ
JUSTA SHINGLE NUMBR PR LIEN KTHX
SUMFIN LIEK THAT: tr -s '[:space:]' '\n'
TLDR, I HAS A TEZTS, GIMMEH TEZTS
TEZTS IS NOW A NUMBR

I HAS A COWNTR ITZ 1
IM IN YR BAK
  I HAS A GUGLARZ, GIMMEH GUGLARZ, GUGLARZ IS NOW A NUMBR
  I HAS A CHEETARZ, GIMMEH CHEETARZ, CHEETARZ IS NOW A NUMBR
  I HAS A BEFT, GIMMEH BEFT, BEFT IS NOW A NUMBR
  I HAS A CUONT ITZ 0, CUONT IS NOW A NUMBR

  IM IN YR DJUDJ UPPIN YR BENSH TIL BOTH SAEM BENSH AN GUGLARZ
    I HAS A SHKOR, GIMMEH SHKOR, SHKOR IS NOW A NUMBR
    I HAS A NERMOL ITZ QUOSHUNT OF SUM OF SHKOR AN 2 AN 3
    I HAS A SUSSPISHUS ITZ QUOSHUNT OF SUM OF SHKOR AN 4 AN 3
    I HAS A CHEETAR ITZ ALL OF...
      BOTH SAEM SMALLR OF CHEETARZ AN 1 AN 1 AN...
      BOTH SAEM SHKOR AN BIGGR OF 2 AN SHKOR AN...
      BOTH SAEM SHKOR AN SMALLR OF 28 AN SHKOR AN...
      BOTH SAEM SUSSPISHUS AN BIGGR OF BEFT AN SUSSPISHUS...
    MKAY

    BOTH SAEM BIGGR OF NERMOL AN BEFT AN NERMOL, O RLY?
      YA RLY
        CUONT R SUM OF CUONT AN 1
      MEBBE CHEETAR
        CHEETARZ R DIFF OF CHEETARZ AN 1
        CUONT R SUM OF CUONT AN 1
      OIC
    
  IM OUTTA YR DJUDJ
  VISIBLE "Case #" COWNTR ": " CUONT
  BOTH SAEM COWNTR AN TEZTS, O RLY?
    YA RLY, GTFO
    NO WAI, COWNTR R SUM OF 1 AN COWNTR
  OIC
IM OUTTA YR BAK

KTHXBYE
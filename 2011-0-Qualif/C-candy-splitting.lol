HAI 1.2

OBTW LOLCODE DUN HAZ YARN-SPILTING SO
YA HAFTO PREPROSHES YR INPUHT SO THERZ
JUSTA SHINGLE NUMBR PR LIEN KTHX
SUMFIN LIEK THAT: tr -s '[:space:]' '\n'
TLDR, I HAS A TEZTS, GIMMEH TEZTS
TEZTS IS NOW A NUMBR

HOW DUZ I CUMPARE YR NMBR AN YR NUTHER
FOUND YR BOTH SAEM NUTHER AN MOD OF NMBR AN PRODUKT OF 2 AN NUTHER
IF U SAY SO

I HAS A COWNTR ITZ 1
IM IN YR BAK
  I HAS A CANDEEZ, GIMMEH CANDEEZ
  I HAS A LOOZER ITZ 9999999
  I HAS A SHUM ITZ 0
  I HAS A KINDASHUM ITZ 0
  IM IN YR SUEETS
    CANDEEZ, O RLY?
      YA RLY
        I HAS A CANDEE, GIMMEH CANDEE
        LOOZER R SMALLR OF CANDEE AN LOOZER
        SHUM R SUM OF CANDEE AN SHUM
	I HAS A NUTHERSHUM ITZ 0
	I HAS A POWAR ITZ 1
	IM IN YR BITZ UPPIN YR BIT TIL BOTH SAEM BIT AN 20
	  I HAS A C, I HAS A N
	  CUMPARE CANDEE POWAR, O RLY?
	  YA RLY, CANDEE R DIFF OF CANDEE AN POWAR, C R WIN, OIC
	  CUMPARE KINDASHUM POWAR, O RLY?
	  YA RLY, KINDASHUM R DIFF OF KINDASHUM AN POWAR, N R WIN, OIC
	  WON OF C AN N, O RLY?
	  YA RLY, NUTHERSHUM R SUM OF POWAR AN NUTHERSHUM, OIC
	  POWAR R PRODUKT OF 2 AN POWAR
	IM OUTTA YR BITZ
	KINDASHUM R NUTHERSHUM
        CANDEEZ R DIFF OF CANDEEZ AN 1
      NO WAI, GTFO
    OIC
  IM OUTTA YR SUEETS
  BOTH SAEM KINDASHUM AN 0, O RLY?
    YA RLY, VISIBLE "Case #" COWNTR ": " DIFF OF SHUM AN LOOZER
    NO WAI, VISIBLE "Case #" COWNTR ": NO"
  OIC
  BOTH SAEM COWNTR AN TEZTS, O RLY?
    YA RLY, GTFO
    NO WAI, COWNTR R SUM OF 1 AN COWNTR
  OIC
IM OUTTA YR BAK
KTHXBYE
